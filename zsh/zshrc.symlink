zmodload zsh/zprof

#############################
### General configuration ###
#############################

setopt no_beep
setopt interactive_comments
setopt prompt_subst
setopt auto_cd
setopt auto_pushd
setopt pushd_ignore_dups
setopt pushd_minus
setopt pushd_silent
setopt append_history
setopt inc_append_history
setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_ignore_space
setopt share_history
setopt hup
setopt long_list_jobs
setopt notify
unsetopt nomatch

# Initialize Powerlevel10k instant prompt for faster startup
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  typeset -g POWERLEVEL9K_INSTANT_PROMPT=quiet
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Cache directory setup
ZSH_CACHE_DIR="${ZSH_CACHE_DIR:-$HOME/.cache/zsh}"
mkdir -p "$ZSH_CACHE_DIR"
ZSH_COMPDUMP="$ZSH_CACHE_DIR/.zcompdump-$SHORT_HOST-$ZSH_VERSION"

# Skip global compinit - we'll handle it ourselves
skip_global_compinit=1
zstyle ':completion:*' rehash true
zstyle ':completion:*' menu select
zstyle ':completion:*:default' list-colors ''
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' completer _complete _approximate
zstyle ':completion:*:approximate:*' max-errors 1 numeric
zstyle -e ':completion:*:approximate:*' max-errors 'reply=($((($#PREFIX+$#SUFFIX)/3))numeric)'
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01'

##############################
### FAST compinit settings ###
##############################

# Fast compinit - always use cache, rebuild daily in background
autoload -Uz compinit
compinit -C -d "${ZSH_COMPDUMP}"

# Background: rebuild if older than 1 day, then compile
{
  setopt localoptions extendedglob
  local dump="${ZSH_COMPDUMP}"
  # Rebuild if older than 1 day
  if [[ -f "$dump" ]] && [[ $(find "$dump" -mtime +1 2>/dev/null) ]]; then
    compinit -d "$dump"
  fi
  # Compile if needed
  if [[ -s "$dump" && (! -s "$dump.zwc" || "$dump" -nt "$dump.zwc") ]]; then
    zcompile "$dump"
  fi
} &!

##############################
### Oh-My-Zsh and Plugins ###
##############################

# Basic zsh settings
setopt no_nomatch
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# Oh-My-Zsh configuration
export ZSH="$HOME/.oh-my-zsh"
ulimit -n 4096
DISABLE_AUTO_UPDATE="true"
DISABLE_MAGIC_FUNCTIONS="true"
ZSH_DISABLE_COMPFIX="true"  # Skip compaudit (saves ~20% startup time)
ZSH_THEME="powerlevel10k/powerlevel10k"
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=30'
ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=20
ZSH_AUTOSUGGEST_USE_ASYNC=1
ZSH_AUTOSUGGEST_STRATEGY=(history completion)

# you-should-use settings
export YSU_MESSAGE_POSITION="after"
export YSU_MODE=ALL

# zsh-auto-notify settings
export AUTO_NOTIFY_THRESHOLD=10  # seconds
export AUTO_NOTIFY_IGNORE=(vim nvim less man watch tmux ssh top htop)

# Plugin loading - reduced eager loading for faster startup
EAGER_PLUGINS=(
  git
  zsh-lazyload
  fast-syntax-highlighting
)

PROMPT_PLUGINS=(
  zsh-autosuggestions
)

KEYPRESS_PLUGINS=(
  zsh-vi-mode
  fzf-tab
  zsh-autopair
)

DEFERRED_PLUGINS=(
  colored-man-pages
  copyfile
  gitignore
  jsontools
  man
  sudo
  web-search
  pnpm
  wd
  # you-should-use    # Reminds you of existing aliases
  zsh-auto-notify     # Desktop notifications for long commands
)

# Set plugins for oh-my-zsh - minimal initial load
plugins=("${EAGER_PLUGINS[@]}")

ZSH_WEB_SEARCH_ENGINES=(
  anna "https://tw.annas-archive.org/search?index=&ext=epub&sort=newest&q="
  pubmed "https://pubmed-ncbi-nlm-nih-gov.autorpa.kfsyscc.org/?sort=date&term="
  cco "https://clinicaloptions.com/CE-CME?specialities=hematology,oncology&contenttypes=pdf,slideset,slideset-download,text-module&searchtext="
  scihub "https://sci-hub.tw/"
  search-uptodate "https://www-uptodate-com.autorpa.kfsyscc.org/contents/search?search="
  zlib "https://zh.singlelogin.re/s/"
  google-pdf "https://www.google.com/search?q=filetype%3Apdf+"
)

# Load oh-my-zsh
source "$ZSH/oh-my-zsh.sh"

##############################
### Plugin Lazy Loading    ###
##############################

# Function to find plugin paths with error handling
_find_plugin_path() {
  local plugin=$1
  [[ -z "$plugin" || -z "$ZSH" ]] && return 1

  local plugin_paths=(
    "$ZSH/custom/plugins/$plugin/$plugin.plugin.zsh"
    "$ZSH/custom/plugins/$plugin/${plugin}.zsh"
    "$ZSH/custom/plugins/$plugin/${plugin#zsh-}.plugin.zsh"  # zsh-auto-notify → auto-notify.plugin.zsh
    "$ZSH/plugins/$plugin/$plugin.plugin.zsh"
  )

  for path in "${plugin_paths[@]}"; do
    if [[ -f "$path" && -r "$path" ]]; then
      echo "$path"
      return 0
    fi
  done
  return 1
}

# Prompt-based loading with error handling
_load_prompt_plugins() {
  _load_prompt_plugins() { :; }
  for plugin in "${PROMPT_PLUGINS[@]}"; do
    local plugin_path=$(_find_plugin_path "$plugin")
    if [[ -n "$plugin_path" ]]; then
      source "$plugin_path" 2>/dev/null || echo "Warning: Failed to load prompt plugin: $plugin" >&2
    fi
  done
}
autoload -Uz add-zsh-hook
add-zsh-hook precmd _load_prompt_plugins

# Keypress-based loading with error handling
_load_keypress_plugins() {
  zle -D zle-line-init 2>/dev/null
  for plugin in "${KEYPRESS_PLUGINS[@]}"; do
    local plugin_path=$(_find_plugin_path "$plugin")
    if [[ -n "$plugin_path" ]]; then
      source "$plugin_path" 2>/dev/null || echo "Warning: Failed to load keypress plugin: $plugin" >&2
    fi
  done
}
zle -N zle-line-init _load_keypress_plugins

# Deferred plugin loading - loads once after first command
_load_deferred_plugins() {
  # Self-destruct: only run once
  _load_deferred_plugins() { :; }

  # Load plugins synchronously (runs after first prompt displayed)
  for plugin in "${DEFERRED_PLUGINS[@]}"; do
    local plugin_path=$(_find_plugin_path "$plugin")
    if [[ -n "$plugin_path" ]]; then
      source "$plugin_path" 2>/dev/null
    fi
  done
}
add-zsh-hook precmd _load_deferred_plugins

##############################
### Extra Configs          ###
##############################

export ENABLE_LSP_TOOLS=1


# 1Password CLI plugins
[[ -f "$HOME/.config/op/plugins.sh" ]] && source "$HOME/.config/op/plugins.sh"

# Key bindings
bindkey -s '^o' 'ya\n'
bindkey -s '^n' 'tmux new-window\n'

# Source modules
source "$DOTFILES/zsh/modules/alias.zsh"
source "$DOTFILES/zsh/modules/fzf.zsh"
source "$DOTFILES/zsh/modules/note_related.zsh"

# Source function modules (split from functions.zsh)
source "$DOTFILES/zsh/modules/fn_navigation.zsh"
source "$DOTFILES/zsh/modules/fn_git.zsh"
source "$DOTFILES/zsh/modules/fn_fzf.zsh"
source "$DOTFILES/zsh/modules/fn_utils.zsh"
source "$DOTFILES/zsh/modules/fn_dev.zsh"
source "$DOTFILES/zsh/modules/fn_notes.zsh"
source "$DOTFILES/zsh/modules/fn_media.zsh"
source "$DOTFILES/zsh/modules/fn_secrets.zsh"
[[ -n "$IS_MAC" ]] && source "$DOTFILES/zsh/modules/fn_macos.zsh"

source "$HOME/.uvv"

# Lazy load external tools
# Lazy load fnm (Node version manager)
_load_fnm() {
  if [[ -n "$IS_MAC" ]]; then
    local fnm_path="$HOME/Library/Application Support/fnm"
  else
    local fnm_path="$HOME/.local/share/fnm"
  fi
  if [[ -d "$fnm_path" ]]; then
    export PATH="$fnm_path:$PATH"
    eval "$(fnm env)"
  fi
}

# Lazy load broot
_load_broot() {
  [[ -f "$HOME/.config/broot/launcher/bash/br" ]] && source "$HOME/.config/broot/launcher/bash/br"
}
# lazyload fnm node npm -- _load_fnm
# lazyload br -- _load_broot

# Paths - cached checks for better performance
_cached_path_check() {
  local cache_file="$ZSH_CACHE_DIR/.path_cache"
  if [[ ! -f "$cache_file" || "$cache_file" -ot ~/.zshrc ]]; then
    {
      [[ -d "$HOME/.codeium/windsurf/bin" ]] && echo "CODEIUM_EXISTS=1"
      [[ -d "$HOME/.cache/lm-studio/bin" ]] && echo "LMSTUDIO_EXISTS=1"
      # pnpm path differs by OS
      if [[ -n "$IS_MAC" ]]; then
        [[ -d "$HOME/Library/pnpm" ]] && echo "PNPM_EXISTS=1"
      else
        [[ -d "$HOME/.local/share/pnpm" ]] && echo "PNPM_EXISTS=1"
      fi
    } > "$cache_file"
  fi
  source "$cache_file"
}
_cached_path_check

# Add paths only if they exist
[[ "$CODEIUM_EXISTS" == "1" ]] && export PATH="$HOME/.codeium/windsurf/bin:$PATH"
[[ "$LMSTUDIO_EXISTS" == "1" ]] && export PATH="$PATH:$HOME/.cache/lm-studio/bin"
if [[ "$PNPM_EXISTS" == "1" ]]; then
  if [[ -n "$IS_MAC" ]]; then
    export PNPM_HOME="$HOME/Library/pnpm"
  else
    export PNPM_HOME="$HOME/.local/share/pnpm"
  fi
  case ":$PATH:" in
    *":$PNPM_HOME:"*) ;;
    *) export PATH="$PNPM_HOME:$PATH" ;;
  esac
fi

# Deferred initialization for heavy tools
_deferred_init() {
  # Forgit (deferred)
  if command -v brew >/dev/null 2>&1; then
    local forgit_path="$(brew --prefix)/share/forgit"
    export PATH="$PATH:$forgit_path"
    [[ -f "$HOMEBREW_PREFIX/share/forgit/forgit.plugin.zsh" ]] && source "$HOMEBREW_PREFIX/share/forgit/forgit.plugin.zsh"
  fi

}


# Schedule deferred initialization (silent background)
(sleep 0.1 && _deferred_init) &!

# Python venv
if [ -f ".venv/bin/activate" ]; then
  source .venv/bin/activate
  # Notification (OS-specific)
  if [[ -n "$IS_MAC" ]]; then
    screen -dm bash -c 'osascript -e "display notification \"uv .venv is activated ✨\" with title \"Python Venv\"" >/dev/null 2>&1'
  elif command -v notify-send &>/dev/null; then
    notify-send "Python Venv" "uv .venv is activated ✨" 2>/dev/null &!
  fi
fi

# Load Tmux Layout
if [ -n "$TMUX" ] && [ -f ./tmux.layout.conf ]; then
  panes=$(tmux list-panes | wc -l)
  if [ "$panes" -eq 1 ]; then
    tmux source-file ./tmux.layout.conf
  fi
fi

# Debug Zprof
if [ -n "${ZSH_DEBUGRC+1}" ]; then
  zprof
fi

# Lazy load zoxide (saves ~50ms)
_init_zoxide() {
  unfunction _init_zoxide z zi 2>/dev/null
  eval "$(zoxide init zsh)"
}
z()  { _init_zoxide; z "$@" }
zi() { _init_zoxide; zi "$@" }

# Lazy load atuin - triggers on Up arrow or Ctrl+R
_atuin_loaded=0
_init_atuin_and_up() {
  if (( ! _atuin_loaded )); then
    _atuin_loaded=1
    if [[ -f "$HOME/.atuin/bin/env" ]]; then
      . "$HOME/.atuin/bin/env"
      eval "$(atuin init zsh)"
    fi
  fi
  # Call the real up-line-or-history or atuin widget
  if (( $+functions[_atuin_up_search_widget] )); then
    _atuin_up_search_widget
  else
    zle up-line-or-history
  fi
}
zle -N _init_atuin_and_up

_init_atuin_and_search() {
  if (( ! _atuin_loaded )); then
    _atuin_loaded=1
    if [[ -f "$HOME/.atuin/bin/env" ]]; then
      . "$HOME/.atuin/bin/env"
      eval "$(atuin init zsh)"
    fi
  fi
  if (( $+functions[_atuin_search_widget] )); then
    _atuin_search_widget
  else
    zle history-incremental-search-backward
  fi
}
zle -N _init_atuin_and_search

# Bind Up arrow and Ctrl+R to lazy-load atuin
bindkey '^[[A' _init_atuin_and_up      # Up arrow
bindkey '^[OA' _init_atuin_and_up      # Up arrow (alternate)
bindkey '^r' _init_atuin_and_search    # Ctrl+R
bindkey -M vicmd 'k' _init_atuin_and_up # Vi mode: k for up

# Lazy load direnv - triggers on cd
_direnv_loaded=0
_init_direnv() {
  if (( ! _direnv_loaded )); then
    _direnv_loaded=1
    if command -v direnv &>/dev/null; then
      eval "$(direnv hook zsh)"
    fi
  fi
}
add-zsh-hook chpwd _init_direnv
# Also init if .envrc exists in current dir
[[ -f .envrc ]] && _init_direnv

export PATH=~/.npm-global/bin:$PATH

# Added by Antigravity
export PATH="$HOME/.antigravity/antigravity/bin:$PATH"

# Background compile modules for faster future loads
{
  for f in "$DOTFILES/zsh/modules"/*.zsh; do
    if [[ -f "$f" && (! -f "$f.zwc" || "$f" -nt "$f.zwc") ]]; then
      zcompile "$f"
    fi
  done
} &!

export RUST_LOG=himalaya=error

# Gmail sync alias (run to fetch new emails)
alias gsync='mbsync gmail gmail-work && notmuch new'

# Fix Himalaya UTF-8 truncation bug
export PATH="$HOME/.local/bin:$PATH"
